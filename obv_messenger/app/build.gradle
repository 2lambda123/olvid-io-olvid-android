import org.gradle.internal.os.OperatingSystem
OperatingSystem os = OperatingSystem.current()

apply plugin: 'com.android.application'
apply plugin: 'androidx.navigation.safeargs'
apply plugin: 'com.google.android.gms.oss-licenses-plugin'
apply plugin: 'com.google.protobuf'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'org.jetbrains.kotlin.android'

android {
    compileSdk 33

    defaultConfig {
        manifestPlaceholders
        applicationId "io.olvid.messenger"
        minSdkVersion 21
        targetSdk 33
        versionCode 195
        versionName "0.13.3"
        vectorDrawables.useSupportLibrary true
        multiDexEnabled true
        resConfigs 'en', 'fr'

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [
                        "room.schemaLocation": "$projectDir/schemas".toString(),
                        "room.incremental": "true"
                ]
            }
        }
        manifestPlaceholders.appAuthRedirectScheme = "olvid.openid"

        /////////////
        // Switch this settings to "false" to disable SSL handshake verification altogether.
        // - when "true", Olvid will pin all SSL certificates it encounters so that it may warn the user when a certificate changes
        // - in some contexts (typically with an SSL proxy configured at the OS level) this handshake verification may cause issues
        // If unsure, leave this "true".
        buildConfigField "boolean", "ENABLE_SSL_HANDSHAKE_VERIFICATION", "true"
    }

    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }
    packagingOptions {
        resources {
            excludes += ['META-INF/DEPENDENCIES']
        }
    }


    buildToolsVersion '33.0.0'

    buildTypes {
        release {
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    buildFeatures {
        dataBinding = true
    }

    flavorDimensions "features", "google-services"
    productFlavors {
        prod {
            dimension "features"
            buildConfigField "String", "HARDCODED_API_KEY", "\"00000000-0000-0000-0000-000000000000\""
            buildConfigField "String", "CONTENT_PROVIDER_URI_PREFIX", "\"content://io.olvid.messenger.PROVIDER/\""
            buildConfigField "String", "SERVER_NAME", "\"https://server.olvid.io\""
            buildConfigField "String", "KEYCLOAK_REDIRECT_URL", "\"https://openid-redirect.olvid.io/\""
            buildConfigField "int", "LOG_LEVEL", "io.olvid.engine.Logger.INFO"
            manifestPlaceholders = [
                    KEYCLOAK_REDIRECT_HOST: "openid-redirect.olvid.io"
            ]
        }

        full {
            dimension "google-services"

            buildConfigField "boolean", "USE_BILLING_LIB", "true"
            buildConfigField "boolean", "USE_FIREBASE_LIB", "true"
            buildConfigField "boolean", "USE_GOOGLE_LIBS", "true"
        }

        nogoogle {
            dimension "google-services"
            applicationIdSuffix '.nogoogle'
            versionNameSuffix '-nogoogle'

            buildConfigField "boolean", "USE_BILLING_LIB", "false"
            buildConfigField "boolean", "USE_FIREBASE_LIB", "false"
            buildConfigField "boolean", "USE_GOOGLE_LIBS", "false"
        }
    }

    namespace 'io.olvid.messenger'
}


dependencies {
    annotationProcessor 'androidx.room:room-compiler:2.5.0'

    implementation fileTree(include: ['*.jar', '*.aar'], dir: 'libs')
    implementation project(path: ':libwebrtc')
    implementation project(path: ':sardine-android')
    implementation project(':engine')


    implementation 'com.google.android.material:material:1.8.0'

    // starting with zxing 3.4.0, API level 24 is required...
    implementation 'com.google.zxing:core:3.3.3'

    // starting with jackson 2.14, API level 26 is required...
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.13.4'

    implementation 'androidx.appcompat:appcompat:1.6.0'
    implementation 'androidx.emoji2:emoji2:1.2.0'
    implementation 'androidx.emoji2:emoji2-bundled:1.2.0'
    implementation 'androidx.activity:activity:1.6.1'
    implementation 'androidx.biometric:biometric:1.1.0'
    implementation 'androidx.camera:camera-camera2:1.2.1'
    implementation 'androidx.camera:camera-lifecycle:1.2.1'
    implementation 'androidx.camera:camera-view:1.2.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.core:core-splashscreen:1.0.0'
    implementation 'androidx.core:core:1.9.0'
    implementation 'androidx.exifinterface:exifinterface:1.3.5'
    implementation 'androidx.fragment:fragment:1.5.5'
    implementation 'androidx.lifecycle:lifecycle-process:2.5.1'
    implementation 'androidx.media2:media2-player:1.2.1'
    implementation 'androidx.media2:media2-session:1.2.1'
    implementation 'androidx.media2:media2-widget:1.2.1'
    implementation 'androidx.media:media:1.6.0'
    implementation 'androidx.navigation:navigation-fragment:2.5.3'
    implementation 'androidx.navigation:navigation-ui:2.5.3'
    implementation 'androidx.preference:preference:1.2.0'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'androidx.room:room-runtime:2.5.0'
    implementation 'androidx.sharetarget:sharetarget:1.2.0'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    implementation 'androidx.work:work-runtime:2.7.1'

    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.4'
    implementation 'org.jsoup:jsoup:1.15.3'

    // webclient
    implementation 'com.google.protobuf:protobuf-lite:3.0.1'

    // openID
    implementation 'net.openid:appauth:0.11.1'
    implementation 'org.bitbucket.b_c:jose4j:0.9.2'

    ///////////
    // Google closed source libs
    ///////////

    // Billing
    fullImplementation 'com.android.billingclient:billing:4.1.0'

    // Firebase
    fullImplementation('com.google.firebase:firebase-messaging:23.1.1') {
        exclude group: 'com.google.firebase', module: 'firebase-core'
        exclude group: 'com.google.firebase', module: 'firebase-analytics'
        exclude group: 'com.google.firebase', module: 'firebase-measurement-connector'
    }

    // Google Drive
    fullImplementation 'com.google.android.gms:play-services-auth:20.4.1'
    fullImplementation('com.google.http-client:google-http-client-gson:1.42.3') {
        exclude group: 'org.apache.httpcomponents'
    }
    fullImplementation('com.google.api-client:google-api-client-android:2.1.3') {
        exclude group: 'org.apache.httpcomponents'
    }
    fullImplementation('com.google.apis:google-api-services-drive:v3-rev20221219-2.0.0') {
        exclude group: 'org.apache.httpcomponents'
    }

    // opensource license page
    fullImplementation 'com.google.android.gms:play-services-oss-licenses:17.0.0'
}

protobuf {
    protoc {
        if (os.isMacOsX()) {
            artifact = 'com.google.protobuf:protoc:3.18.0:osx-x86_64'
        } else {
            artifact = 'com.google.protobuf:protoc:3.18.0'
        }
    }
    plugins {
        javalite {
            if (os.isMacOsX()) {
                artifact = 'com.google.protobuf:protoc-gen-javalite:3.0.0:osx-x86_64'
            } else {
                artifact = 'com.google.protobuf:protoc-gen-javalite:3.0.0'
            }
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {
                    remove java
                }
                task.plugins {
                    javalite { }
                }
            }
        }
    }
}

// disable google services plugin task for nogoogle flavor
android.applicationVariants.all { variant ->
    def googleTask = tasks.findByName("process${variant.name.capitalize()}GoogleServices")
    googleTask.enabled = !variant.flavorName.contains("Nogoogle")
}