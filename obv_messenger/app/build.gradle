plugins {
    id 'com.android.application'
    id 'androidx.navigation.safeargs'
    id 'com.google.android.gms.oss-licenses-plugin'
    id 'com.google.protobuf'
}

android {
    compileSdkVersion 31

    defaultConfig {
        applicationId "io.olvid.messenger"
        minSdkVersion 21
        targetSdkVersion 31
        versionCode 156
        versionName "0.11.1"
        vectorDrawables.useSupportLibrary true
        multiDexEnabled true

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [
                        "room.schemaLocation": "$projectDir/schemas".toString(),
                        "room.incremental": "true"
                ]
            }
        }
        manifestPlaceholders.appAuthRedirectScheme = "olvid.openid"
    }

    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }
    packagingOptions {
        resources {
            excludes += ['META-INF/DEPENDENCIES']
        }
    }


    buildToolsVersion '31.0.0'

    buildTypes {
        release {
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    buildFeatures {
        dataBinding = true
    }

    flavorDimensions "features"
    productFlavors {
        prod {
            dimension "features"
            buildConfigField "String", "HARDCODED_API_KEY", "\"00000000-0000-0000-0000-000000000000\""
            buildConfigField "String", "CONTENT_PROVIDER_URI_PREFIX", "\"content://io.olvid.messenger.PROVIDER/\""
            buildConfigField "String", "SERVER_NAME", "\"https://server.olvid.io\""
            buildConfigField "String", "KEYCLOAK_REDIRECT_URL", "\"https://openid-redirect.olvid.io/\""
            buildConfigField "int", "LOG_LEVEL", "io.olvid.engine.Logger.INFO"
            manifestPlaceholders = [
                    KEYCLOAK_REDIRECT_HOST : "openid-redirect.olvid.io"
            ]
        }
    }
}


dependencies {
    annotationProcessor 'androidx.room:room-compiler:2.4.1'

    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation project(path: ':libwebrtc')
    implementation project(':engine')


    // for latest versions see https://github.com/material-components/material-components-android/releases
    implementation 'com.google.android.material:material:1.5.0'
    implementation('com.google.firebase:firebase-messaging:23.0.0') {
        exclude group: 'com.google.firebase', module: 'firebase-core'
        exclude group: 'com.google.firebase', module: 'firebase-analytics'
        exclude group: 'com.google.firebase', module: 'firebase-measurement-connector'
    }
    // starting with zxing 3.4.0, API level 24 is required...
    implementation 'com.google.zxing:core:3.3.3'
    implementation 'com.android.billingclient:billing:4.0.0'
    implementation 'com.fasterxml.jackson.core:jackson-core:2.13.1'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.13.1'

    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'androidx.emoji2:emoji2:1.0.1'
    implementation 'androidx.emoji2:emoji2-bundled:1.0.1'
    implementation 'androidx.activity:activity:1.4.0'
    implementation 'androidx.biometric:biometric:1.1.0'
    implementation 'androidx.camera:camera-camera2:1.0.2'
    implementation 'androidx.camera:camera-lifecycle:1.0.2'
    implementation 'androidx.camera:camera-view:1.0.0-alpha32'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'
    implementation 'androidx.core:core-splashscreen:1.0.0-beta01'
    implementation 'androidx.core:core:1.7.0'
    implementation 'androidx.exifinterface:exifinterface:1.3.3'
    implementation 'androidx.fragment:fragment:1.4.0'
    implementation 'androidx.lifecycle:lifecycle-process:2.4.0'
    implementation 'androidx.media2:media2-player:1.2.0'
    implementation 'androidx.media2:media2-session:1.2.0'
    implementation 'androidx.media2:media2-widget:1.2.0'
    implementation 'androidx.media:media:1.4.3'
    implementation 'androidx.navigation:navigation-fragment:2.3.5'
    implementation 'androidx.navigation:navigation-ui:2.3.5'
    implementation 'androidx.preference:preference:1.1.1'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'androidx.room:room-runtime:2.4.1'
    implementation 'androidx.sharetarget:sharetarget:1.2.0-rc01'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    implementation 'androidx.work:work-runtime:2.7.1'

    // generation of the opensource license page
    implementation 'com.google.android.gms:play-services-oss-licenses:17.0.0'

    // connection to google drive
    implementation 'com.google.android.gms:play-services-auth:20.0.1'
    implementation('com.google.http-client:google-http-client-gson:1.41.0') {
        exclude group: 'org.apache.httpcomponents'
    }
    implementation('com.google.api-client:google-api-client-android:1.33.0') {
        exclude group: 'org.apache.httpcomponents'
    }
    // for latest version, see: https://github.com/googleapis/google-api-java-client-services/tree/master/clients/google-api-services-drive/v3
    implementation('com.google.apis:google-api-services-drive:v3-rev20220110-1.32.1') {
        exclude group: 'org.apache.httpcomponents'
    }

    // webclient
    implementation 'org.java-websocket:Java-WebSocket:1.5.2'
    implementation 'com.google.protobuf:protobuf-lite:3.0.1'

    // openID
    implementation 'net.openid:appauth:0.11.1'
    implementation 'org.bitbucket.b_c:jose4j:0.7.9'
}

apply plugin: 'com.google.gms.google-services'

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.18.0'
    }
    plugins {
        javalite {
            artifact = 'com.google.protobuf:protoc-gen-javalite:3.0.0'
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {
                    remove java
                }
                task.plugins {
                    javalite { }
                }
            }
        }
    }
}